/*
	CSC's Common Network Services Library

	Description: Private library for common network services used in my apps. Not designed for more generalized use.

	Creation Date: 2020-05-16

	Author: Curtis Collins ... with much copied from others

*/

#ifdef ESP8266 
   #include <ESP8266WiFi.h> 
   #include <ESP8266mDNS.h>
#endif

#ifdef ESP32 
  #include <WiFi.h> 
  #include <ESPmDNS.h>
#endif


#include <WiFiUdp.h>
#include <PubSubClient.h>
#include <ArduinoJson.h>
#include <TimeLib.h>


#define MSGBUFFLEN 300
#define WIFIRETRYCNT 5
#define WIFIRETRYDELAY 5000
#define WIFITRYALT false
#define WIFIREBOOT true

#define MQTTRETRYDELAY 5000
#define MQTTRETRYCOUNT 5

#define PSSID "COWIFI151420636/0"
#define PPWD "WiFi-89951645"
#define ALTSSID "Bodega"
#define ALTPWD "Sail2012"

bool debug = true;

const char* ssid = PSSID;
const char* pwd = PPWD;

int mqttPort = 1883;
int hostEntry = -1;
int msgn = 0;

const char* mqttTopic = "(empty)";

char nodeName[24] = "";
char msgbuff[MSGBUFFLEN] = "";

WiFiClient wifiClient;
PubSubClient mqttClient(wifiClient);
WiFiUDP Udp;
unsigned int localPort = 8888;  // local port to listen for UDP packets

//
// NTP Servers defs
//

static const char ntpServerName[] = "us.pool.ntp.org";
//static const char ntpServerName[] = "time.nist.gov";
//static const char ntpServerName[] = "time-a.timefreq.bldrdoc.gov";
//static const char ntpServerName[] = "time-b.timefreq.bldrdoc.gov";
//static const char ntpServerName[] = "time-c.timefreq.bldrdoc.gov";

//
// Time Zones
//

//const int timeZone = 1;     // Central European Time
const int timeZone = -5;  // Eastern Standard Time (USA)
//const int timeZone = -4;  // Eastern Daylight Time (USA)
//const int timeZone = -8;  // Pacific Standard Time (USA)
//const int timeZone = -7;  // Pacific Daylight Time (USA)


//
// Forward Declaratioins
//

time_t getNtpTime();
void sendNTPpacket(IPAddress& address);

//
// General fucntions
//

void outputMsg(char* msg)
{
	Serial.println(msg);
}

//
// WiFi Services
//

void connectWiFi()
{
	int retryCnt = 0;
	bool wifiAltTried = false;

//	WiFi.hostname(nodeName);
	WiFi.mode(WIFI_STA);
	Serial.print(F("\nWiFi connecting to "));
	Serial.println(ssid);
	WiFi.begin(ssid, pwd);
	while ((WiFi.status() != WL_CONNECTED) && (retryCnt < WIFIRETRYCNT))
	{
		delay(WIFIRETRYDELAY);
		retryCnt++;
		if ((retryCnt >= WIFIRETRYCNT) && WIFITRYALT && !wifiAltTried)
		{
			ssid = ALTSSID;
			pwd = ALTPWD;
			retryCnt = 0;
			Serial.print(F("\nWiFi connecting to alt SSID... "));
			Serial.println(ssid);
			WiFi.begin(ssid, pwd);
			wifiAltTried = true;
		};
	}

	if (WiFi.status() == WL_CONNECTED)
	{
		Serial.print(F("\nWiFi connected to "));
		Serial.print(ssid);
		Serial.print(F(" "));
		Serial.println(WiFi.localIP());
	}
	else
	{
		if (WIFIREBOOT)
		{
			Serial.println(F("WiFi not connected, rebooting..."));
			ESP.restart();
		}
		else
		{
			Serial.println(F("WiFi not connected."));
		}
	}
}

//
// mDNS Services
//

bool setupMdns(char* nodeName)
{
	if (!MDNS.begin(nodeName))
	{
		Serial.println("Error setting up MDNS responder!");
		return(false);
	}
	return(true);
}

int findService(char* serviceType, char* protocol)
{
	int n = 0;
	n = MDNS.queryService(serviceType, protocol); // Send out query for esp tcp services
	if (n == 0)
	{
		Serial.println("no services found");
	}
	else
	{
		msgn = snprintf(msgbuff, MSGBUFFLEN, "%i service(s) found, service 0: ", n);
		Serial.print(msgbuff);
		Serial.print(MDNS.hostname(0));
		Serial.print(" ");
		Serial.print(MDNS.IP(0));
		Serial.print(":");
		Serial.println(MDNS.port(0));
		return (0); // return index to 1st service only
	}
	return(-1);  // no service found
}

//
// MQTT Services
//


void connectMQTT(bool subscribe, IPAddress server)
{
	int retry_cnt = 0;

	Serial.print("\nConnecting to MQTT Server: ");
	Serial.println(server);
	while (!mqttClient.connected()) {

		if (mqttClient.connect(nodeName))
		{
			Serial.println("MQTT connected");
			if (subscribe)
			{
				if (!mqttClient.subscribe(mqttTopic))
				{
					Serial.print("\nMQTT subscription to ");
					Serial.print(mqttTopic);
					Serial.println(" failed!");
					
				}
				else
				{
				   Serial.print("MQTT mqttTopic  subscribed: ");
				   Serial.print(mqttTopic);
				}
			}

			retry_cnt = 0;
		}
		else
		{
			Serial.print("failed, rc=");
			Serial.print(mqttClient.state());
			Serial.println(" try again in 5 seconds");
			delay(MQTTRETRYDELAY);
			retry_cnt++;
		}

		if (retry_cnt > MQTTRETRYCOUNT)
		{
			Serial.println();
			Serial.println("Rebooting...");
			ESP.restart();
		}
	}
}

void setupMQTT(IPAddress mqttserver, int mqttPort, bool mqttsubscribe, std::function<void(char*, uint8_t*, unsigned int)> callback)
{
	Serial.println();
	Serial.println("MQTT Setup... ");
	mqttClient.setServer(mqttserver, mqttPort);
	mqttClient.setCallback(callback);
	connectMQTT(mqttsubscribe, mqttserver);
	if (mqttClient.state() != MQTT_CONNECTED) Serial.println("MQTT NOT connected!");
}

//
// OTA Services
//


//
// NTP Services
//

void setupNTP()
{
	Serial.println("\nNTP Setup...\n\nwaiting for NTP server sync");
	setSyncProvider(getNtpTime);
	setSyncInterval(300);
}

const int NTP_PACKET_SIZE = 48; // NTP time is in the first 48 bytes of message
byte packetBuffer[NTP_PACKET_SIZE]; //buffer to hold incoming & outgoing packets

time_t getNtpTime()
{
#define NTPRETRYCOUNT 3
	IPAddress ntpServerIP; // NTP server's ip address
	int i = 0;


	while (Udp.parsePacket() > 0); // discard any previously received packets

	while (i++ < NTPRETRYCOUNT)
	{
		Serial.println("Sending NTP Request");
		// get a random server from the pool
		WiFi.hostByName(ntpServerName, ntpServerIP);
		Serial.print(ntpServerName);
		Serial.print(": ");
		Serial.println(ntpServerIP);
		sendNTPpacket(ntpServerIP);
		uint32_t beginWait = millis();
		while (millis() - beginWait < 3000)
		{
			int size = Udp.parsePacket();
			if (size >= NTP_PACKET_SIZE)
			{
				Serial.println("Received NTP Response");
				Udp.read(packetBuffer, NTP_PACKET_SIZE);  // read packet into the buffer
				unsigned long secsSince1900;
				// convert four bytes starting at location 40 to a long integer
				secsSince1900 = (unsigned long)packetBuffer[40] << 24;
				secsSince1900 |= (unsigned long)packetBuffer[41] << 16;
				secsSince1900 |= (unsigned long)packetBuffer[42] << 8;
				secsSince1900 |= (unsigned long)packetBuffer[43];
				return secsSince1900 - 2208988800UL + timeZone * SECS_PER_HOUR;
			}
		}
		Serial.println("No NTP Response, retrying");
	}

	Serial.println("No NTP Response.");
	return 0; // return 0 if unable to get the time
}

// send an NTP request to the time server at the given address
void sendNTPpacket(IPAddress& address)
{
	// set all bytes in the buffer to 0
	memset(packetBuffer, 0, NTP_PACKET_SIZE);
	// Initialize values needed to form NTP request
	// (see URL above for details on the packets)
	packetBuffer[0] = 0b11100011;   // LI, Version, Mode
	packetBuffer[1] = 0;     // Stratum, or type of clock
	packetBuffer[2] = 6;     // Polling Interval
	packetBuffer[3] = 0xEC;  // Peer Clock Precision
	// 8 bytes of zero for Root Delay & Root Dispersion
	packetBuffer[12] = 49;
	packetBuffer[13] = 0x4E;
	packetBuffer[14] = 49;
	packetBuffer[15] = 52;
	// all NTP fields have been given values, now
	// you can send a packet requesting a timestamp:
	Udp.beginPacket(address, 123); //NTP requests are to port 123
	Udp.write(packetBuffer, NTP_PACKET_SIZE);
	Udp.endPacket();
}

